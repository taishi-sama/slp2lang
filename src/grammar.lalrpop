use crate::ast::*;

grammar;

match {
    r"\s*" => { }, // The default whitespace skipping is disabled if an `ignore pattern` is specified
    r"//[^\n\r]*[\n\r]*" => { }, // Skip `// comments`
    r"/\*[^*]*\*+(?:[^/*][^*]*\*+)*/" => { },  // Skip `/* comments */`

    r"(?i)begin" => Begin,
    r"(?i)end" => End, 
    r"(?i)procedure" => Procedure, 
    r"(?i)function" => Function, 
    r"(?i)var" => Var, 
    r"(?i)array" => Array, 
    r"(?i)of" => Of, 
    r"(?i)if" => If, 
    r"(?i)then" => Then, 
    r"(?i)else" => Else, 
    r"(?i)not" => Not, 
    r"(?i)shl" => Shl,
    r"(?i)shr" => Shr,
    r"(?i)and" => And,
    r"(?i)or" => Or,
    r"(?i)new" => New, 
    r"(?i)uses" => Uses,
    r"(?i)external" => External,
    r"(?i)true" => True,
    r"(?i)false" => False, 
    r"(?i)while" => While,
    r"(?i)do" => Do,
    r"(?i)type" => TypeKW,
    r"(?i)class" => Class,
    r"(?i)record" => Record,
    r"(?i)as" => As,
    r"(?i)defer" => Defer,
    "(",
    ")",
    "[",
    "]",
    ";",
    "::",
    "..",
    ".",
    ":=",
    "<>",
    "<=",
    ">=",
    ">",
    "<",
    "=",
    ":",
    ",",
    "^",
    "@",
    "+",
    "-",
    "*",
    "/",
    
} else {
    r"[a-zA-Z_]+[a-zA-Z_0-9]*" => ID,
    r"([0-9][0-9_]*)" => Int,
    r#""[^\"]*""# => StrLiteral,
    r#"'(([^\\'"])|(\\')|(\\")|(\\n)|(\\r)|(\\t)|(\\\\)|(\\0)|(\\u[0-9a-fA-F]{1,6}))'"# => CharLiteral,
} else {
    r#"(-)?(((0b|0x|0o)[0-9a-fA-F][0-9_a-fA-F]*)|([0-9][0-9_]*))(u8|i8|u16|i16|u32|i32|u64|i64|isize|usize)?"# => FullInt,
}
SoloId:String = <s:ID> => s.to_string();

//pub ProgramBlock: Program = {
//    <pd: ProcedureDecl> <mut p: ProgramBlock> => {p.declarations.insert(0, Declaration::Function(pd)); return p;},
//    <md: MainDecl> <mut p: ProgramBlock> => {p.declarations.insert(0, Declaration::Function(md)); return p;},
//    <fd: FunctionDecl> <mut p: ProgramBlock> => {p.declarations.insert(0, Declaration::Function(fd)); return p;},
//    () => Program{declarations: Vec::new()},
//}
pub ProgramBlock: ProgramFile = {
    <uf: UsingSection?> <decs: Declarations*> => ProgramFile{declarations: decs, uses: uf.unwrap_or(vec![])}
}
pub UsingSection: Vec<Usings> = {
    <tl:@L> Uses <uf: UsingFrag> ";" <tr:@R> => uf
}
pub UsingFrag: Vec<Usings> = {
    <tl:@L> <id: ID> <tr:@R> "," <mut uf: UsingFrag> => {uf.insert(0, Usings::Name(Loc::new(tl, tr), id.to_string())); return uf;},
    <tl:@L> <id: StrLiteral> <tr:@R> "," <mut uf: UsingFrag> => {uf.insert(0, Usings::Path(Loc::new(tl, tr), id[1..id.len()-1].to_string())); return uf;}, 
    <tl:@L> <id: ID> <tr:@R> => vec![Usings::Name(Loc::new(tl, tr), id.to_string())],
    <tl:@L> <id: StrLiteral> <tr:@R> => vec![Usings::Path(Loc::new(tl, tr), id[1..id.len()-1].to_string())], 

}
Declarations: Declaration = {
    <pd: ProcedureDecl>  => Declaration::Function(pd),
    <md: MainDecl> => Declaration::Function(md),
    <fd: FunctionDecl> => Declaration::Function(fd),
    <efd: ExternalFunctionDecl> => Declaration::ExternFunction(efd),
    <tds: TypeDeclSection> => Declaration::TypeDeclSection(tds),
}
TypeDeclSection: TypeDeclSectionBody = {
    <tl:@L> TypeKW <decls: TypeDeclElement+> <tr:@R> => TypeDeclSectionBody{decls}, 
}
TypeDeclElement: TypeDeclElement = {
    <a:TypeAliasElement> => a,
    <a:RecordDeclElement> => a,

}
TypeAliasElement: TypeDeclElement = {
    <tl:@L> <id: SoloId> "=" <al: Type> ";" <tr:@R> => TypeDeclElement::TypeAlias(Loc::new(tl, tr), id, al),
}

RecordDeclElement: TypeDeclElement = {
    <tl:@L> <id: SoloId> "=" Record <rf: RecordField*> End ";" <tr:@R> => TypeDeclElement::RecordDeclare(Loc::new(tl, tr), id, rf),
}

RecordField: RecordField = {
    <tl:@L> <id: SoloId> ":" <ty: TypeDecl> <tr:@R> ";" => RecordField{id, ty, loc: Loc::new(tl, tr)},
}

ProcedureDecl: FunctionBody = {
    <tl:@L> Procedure <f:SoloId> "(" <fa:ArgsList> ")" Begin <sb:StatementsBlock> End ";"<tr:@R> => FunctionBody{loc:Loc::new(tl, tr), function_name: f, body: sb, function_args: fa, return_arg: TypeDecl{loc: Loc::new(tr, tr), ty: Type::void()}}
};
FunctionDecl: FunctionBody = {
    <tl:@L> Function <f:SoloId> "(" <fa:ArgsList> ")" ":" <ret: TypeDecl> Begin <sb:StatementsBlock> End ";"<tr:@R> => FunctionBody{loc:Loc::new(tl, tr), function_name: f, body: sb, function_args: fa, return_arg: ret}
}
ExternalFunctionDecl: ExternFunctionBody = {
    <tl:@L> External Function <f:SoloId> "(" <fa:ArgsList> ")" ":" <ret: TypeDecl> ";"<tr:@R> => ExternFunctionBody{loc:Loc::new(tl, tr), function_name: f, function_args: fa, return_arg: ret}
}
MainDecl: FunctionBody = {
    <tl:@L> Begin <sb:StatementsBlock> End "." <tr:@R> => FunctionBody{loc:Loc::new(tl, tr), function_name: "main".to_string(), body: sb, function_args: vec![], return_arg: TypeDecl{loc: Loc::new(tr, tr), ty: Type::Primitive(Identificator{name: "int32".to_string(), path: vec![]})}}
}

ArgsList: ArgDeclList = {
    <d: ArgsDecl> ";" <mut adl: ArgsList> => {adl.insert(0, d); return adl;},
    <d: ArgsDecl> => vec![d],
    () => Vec::new(),
}
ArgsDecl: ArgDecl = {
    <tl:@L> <q:Var?> <il:IndList> ":" <t:TypeDecl> <tr:@R> => ArgDecl{names: il, ty: t, loc: Loc::new(tl, tr), var_param: q.is_some()}
}
IndList: Vec<String> = {
    <id:SoloId> "," <mut il: IndList> => {il.insert(0, id.clone()); return il;},
    <id:SoloId> => vec![id.clone()]
}

StatementsBlock: StatementBlock = {
    <s:Statement> ";" <mut sb: StatementsBlock> => {sb.insert(0, s); return sb;},
    <s:Statement> => vec![s],
    //() => Vec::new(),
}

Statement: Statement = {
    <p: ClosedStatement> => p,
    <p: OpenStatement> => p,

}
// https://github.com/lalrpop/lalrpop/blob/master/doc/pascal/lalrpop/src/pascal.lalrpop
OpenStatement: Statement = {
    <ifsmt: OpenIfStatement> => ifsmt,
    <whsmt: OpenWhileStatement> => whsmt,
}
ClosedStatement: Statement = {
    <cb:CodeBlock> => cb,
    <lvd: LocalVariableDecl> => lvd,
    <asmt: AssigmentStatement> => asmt,
    <ifsmt: ClosedIfStatement> => ifsmt,
    <whsmt: ClosedWhileStatement> => whsmt,
    <fc: FuncCallStmt> => fc,
    <df: DeferStatement> => df,
    () => Statement::Empty(),

}

DeferStatement: Statement = {
    <tl:@L> Defer <s:ClosedStatement> <tr:@R> => Statement::Defer(Loc::new(tl, tr), Box::new(s))
}

CodeBlock : Statement = {
    <tl:@L> Begin <sb:StatementsBlock> End <tr:@R> => Statement::CodeBlock(Loc::new(tl, tr), sb)
}

LocalVariableDecl: Statement = {
    <tl:@L> Var <il: IndList> ":" <ty:TypeDecl> <tr:@R> => Statement::VarDecl(Loc::new(tl, tr), VarDecl::Multiple(il, ty)),
    <tl:@L> Var <il: IndList> ":" <ty:TypeDecl> ":=" <e: Expr> <tr:@R> => Statement::VarDecl(Loc::new(tl, tr), VarDecl::ExplicitType(il[0].clone(), ty, Box::new(e))),
    <tl:@L> Var <il: IndList> ":" <ty:TypeDecl> "=" <e: Expr> <tr:@R> => Statement::VarDecl(Loc::new(tl, tr), VarDecl::ExplicitType(il[0].clone(), ty, Box::new(e))),
    <tl:@L> Var <il: IndList> ":=" <e: Expr> <tr:@R> => Statement::VarDecl(Loc::new(tl, tr), VarDecl::ImplicitType(il[0].clone(), Box::new(e)))
}
FuncCallStmt: Statement = {
    <tl:@L> <fc:FuncCall> <tr:@R> => Statement::FunctionCall(Loc::new(tl, tr), fc),
}
OpenIfStatement: Statement = {
    <tl:@L> If <cond:Expr> Then <true_stmt: Statement> <tr:@R> => Statement::If(Loc::new(tl, tr), Box::new(cond), Box::new(true_stmt), None),
    <tl:@L> If <cond:Expr> Then <true_stmt: ClosedStatement> Else <false_stmt: OpenStatement> <tr:@R> => Statement::If(Loc::new(tl, tr), Box::new(cond), Box::new(true_stmt), Some(Box::new(false_stmt))),
}
ClosedIfStatement: Statement = {
    <tl:@L> If <cond:Expr> Then <true_stmt: ClosedStatement> Else <false_stmt: ClosedStatement> <tr:@R> => Statement::If(Loc::new(tl, tr), Box::new(cond), Box::new(true_stmt), Some(Box::new(false_stmt))),
}
OpenWhileStatement: Statement = {
    <tl:@L> While <cond:Expr> Do <body_stmt: OpenStatement> <tr:@R> => Statement::While(Loc::new(tl, tr), Box::new(cond), Box::new(body_stmt)),
}
ClosedWhileStatement: Statement = {
    <tl:@L> While <cond:Expr> Do <body_stmt: ClosedStatement> <tr:@R> => Statement::While(Loc::new(tl, tr), Box::new(cond), Box::new(body_stmt)),
}

AssigmentStatement : Statement = {
    <tl:@L> <lhs:Expr> ":=" <rhs:Expr> <tr:@R> => Statement::Assignment(Loc::new(tl, tr), Box::new(lhs), Box::new(rhs))
}


Expr: Expr = {
    Tier5
};


Tier5: Expr = {
    <tl:@L> <e1:Tier5> "<" <e2:Tier4> <tr:@R> => Expr::OpBinLesser(Loc::new(tl, tr), Box::new(e1), Box::new(e2)),
    <tl:@L> <e1:Tier5> ">" <e2:Tier4> <tr:@R> => Expr::OpBinGreater(Loc::new(tl, tr), Box::new(e1), Box::new(e2)),
    <tl:@L> <e1:Tier5> "<=" <e2:Tier4> <tr:@R> => Expr::OpBinLesserEq(Loc::new(tl, tr), Box::new(e1), Box::new(e2)),
    <tl:@L> <e1:Tier5> ">=" <e2:Tier4> <tr:@R> => Expr::OpBinGreaterEq(Loc::new(tl, tr), Box::new(e1), Box::new(e2)),
    <tl:@L> <e1:Tier5> "=" <e2:Tier4> <tr:@R> => Expr::OpBinEq(Loc::new(tl, tr), Box::new(e1), Box::new(e2)),
    <tl:@L> <e1:Tier5> "<>" <e2:Tier4> <tr:@R> => Expr::OpBinNotEq(Loc::new(tl, tr), Box::new(e1), Box::new(e2)),

    Tier4
};


Tier4: Expr = {
    <tl:@L> <e1:Tier4> "+" <e2:Tier3> <tr:@R> => Expr::OpBinPlus(Loc::new(tl, tr), Box::new(e1), Box::new(e2)),
    <tl:@L> <e1:Tier4> "-" <e2:Tier3> <tr:@R> => Expr::OpBinMinus(Loc::new(tl, tr), Box::new(e1), Box::new(e2)),
    <tl:@L> <e1:Tier4> Or <e2:Tier3> <tr:@R> => Expr::OpBinOr(Loc::new(tl, tr), Box::new(e1), Box::new(e2)),
    Tier3
};

Tier3: Expr = {
    <tl:@L> <e1:Tier3> "*" <e2:Tier2> <tr:@R> => Expr::OpBinAsterisk(Loc::new(tl, tr), Box::new(e1), Box::new(e2)),
    <tl:@L> <e1:Tier3> "/" <e2:Tier2> <tr:@R> => Expr::OpBinSlash(Loc::new(tl, tr), Box::new(e1), Box::new(e2)),
    <tl:@L> <e1:Tier3> Shr <e2:Tier2> <tr:@R> => Expr::OpBinShr(Loc::new(tl, tr), Box::new(e1), Box::new(e2)),
    <tl:@L> <e1:Tier3> Shl <e2:Tier2> <tr:@R> => Expr::OpBinShl(Loc::new(tl, tr), Box::new(e1), Box::new(e2)),
    <tl:@L> <e1:Tier3> And <e2:Tier2> <tr:@R> => Expr::OpBinAnd(Loc::new(tl, tr), Box::new(e1), Box::new(e2)),

    Tier2
};

Tier2: Expr = {
    <tl:@L> "@" <e:Tier2> <tr:@R>  => Expr::OpUnGetRef(Loc::new(tl, tr), Box::new(e)),
    <tl:@L> Not <e:Tier2> <tr:@R>  => Expr::OpUnNot(Loc::new(tl, tr), Box::new(e)),
    <tl:@L> <e:Tier1> "^" <tr:@R>  => Expr::OpUnDeref(Loc::new(tl, tr), Box::new(e)),
    <tl:@L> "+" <e:Tier1> <tr:@R>  => Expr::OpUnPlus(Loc::new(tl, tr), Box::new(e)),
    <tl:@L> "-" <e:Tier1> <tr:@R>  => Expr::OpUnMinus(Loc::new(tl, tr), Box::new(e)),
    <tl:@L> <e:Tier1> As <ty:TypeDecl> <tr:@R>  => Expr::OpUnAs(Loc::new(tl, tr), Box::new(e), ty),
    Tier1
};


Tier1: Expr = {
    "(" <e:Expr> ")" => e,
    <tl:@L> <e:Tier1> "." <id:SoloId> <tr:@R> => Expr::OpMethodCall(Loc::new(tl, tr), Box::new(e), id),
    <tl:@L> New <ty:Type> "(" <f:FuncCallArgList> ")"<tr:@R> => Expr::OpNew(Loc::new(tl, tr), ty, f),
    <tl:@L> <e:Tier1> "[" <i:Expr> "]" <tr:@R> => Expr::OpBinIndex(Loc::new(tl, tr), Box::new(e), Box::new(i)),
    <tl:@L> <fc: FuncCall> <tr:@R> => Expr::OpFunctionCall(Loc::new(tl, tr), fc),
    <tl:@L> <c: Constant> <tr:@R> => Expr::Constant(Loc::new(tl, tr), c),
    <tl:@L> <id: IdPath> <tr:@R> => Expr::Ident(Loc::new(tl, tr), id),
    
};

IdPath: Identificator = {
    <id: SoloId> => Identificator{name: id, path: vec![]},
    <path: SoloId> "::" <mut id:IdPath> => {id.path.insert(0, path.clone()); return id;}
}

FuncCall: FunctionCall = {
    <e:Tier1> "(" <f:FuncCallArgList> ")" <tr:@R> => FunctionCall{func: Box::new(e), args: f}
}

Constant: Constant = { 
    <s: StringConstant> => Constant::String(s),
    <i: IntConstant> => Constant::Int(i),
    <c: CharConstant> => Constant::Char(c),
    True => Constant::Bool(true),
    False => Constant::Bool(false),

};
IntConstant: String = {
    <s:Int> => s.to_string(),
    <s:FullInt> => s.to_string()
};
CharConstant: String = <s:CharLiteral> => s.to_string();

StringConstant: String = <s:StrLiteral> => s[1..(s.len()-1)].to_string();

FuncCallArgList: Vec<Expr> = {
    <d: Expr> "," <mut adl: FuncCallArgList> => {adl.insert(0, d); return adl;},
    <d: Expr> => vec![d],
    () => Vec::new(),
}

TypeDecl: TypeDecl = <tl:@L>  <t: Type> <tr:@R> => TypeDecl{ty: t, loc: Loc::new(tl, tr)};

Type: Type = {
    "^"<t: Type> => Type::Pointer(Box::new(t)),
    Array Of <t: Type>  => Type::DynArray(Box::new(t)),
    Array "[" <b: Int> ".." <e: Int> "]" Of <t: Type>  => Type::FixedArray(b.parse().unwrap(), e.parse().unwrap(), Box::new(t)),
    <id: IdPath> => Type::Primitive(id)
};
